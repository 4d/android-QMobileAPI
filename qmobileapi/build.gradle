apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

apply from: "$rootDir/buildscripts/ktlint.gradle"
apply from: "$rootDir/buildscripts/testlog.gradle"
apply from: "$rootDir/buildscripts/detekt.gradle"
apply from: "../buildscripts/jacoco.gradle"
apply from: "../buildscripts/maven-check.gradle"
apply from: "../buildscripts/git-hash.gradle"

dependencies {

    // OkHttp
    implementation Libs.okhttp_jvm
    implementation Libs.okhttp_logging_interceptor
    implementation Libs.okhttp_tls

    // Retrofit
    implementation Libs.retrofit_adapter_rxjava2
    implementation Libs.retrofit_converter_gson

    // Jackson
    implementation Libs.jackson

    // Utils
    implementation Libs.androidx_preference_ktx
    implementation Libs.rxandroid
    implementation Libs.timber

    // Testing
    testImplementation Libs.androidx_core
    testImplementation Libs.androidx_core_testing
    testImplementation Libs.junit
    testImplementation Libs.mockito
    testImplementation Libs.okhttp_mockwebserver
    testImplementation Libs.robolectric
}

def libraryGroupId = 'com.qmobile.qmobileapi'
def libraryArtifactId = 'qmobileapi'
def libraryVersion = Versions.qmobileapi
def buildVariant = 'release'

publishing {
    publications {
        aar(MavenPublication) {
            groupId libraryGroupId
            version libraryVersion + '-' + currentGitBranch()

            artifactId libraryArtifactId
            artifact("$buildDir/outputs/aar/${artifactId}-${buildVariant}.aar")

            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.getByName("${buildVariant}CompileClasspath").getResolvedConfiguration().getFirstLevelModuleDependencies().each {
                    def dependency = dependencies.appendNode('dependency')
                    dependency.appendNode('groupId', it.moduleGroup)
                    dependency.appendNode('artifactId', it.moduleName)
                    dependency.appendNode('version', it.moduleVersion)
                }
            }
        }
    }
}

artifactory {
    def machineIP = System.getenv('ARTIFACTORY_MACHINE_IP')
    contextUrl = "http://${machineIP}:8081/artifactory"
    publish {
        repository {
            repoKey = "libs-release-local"
            username = System.getenv('ARTIFACTORY_USERNAME')
            password = System.getenv('ARTIFACTORY_PASSWORD')
        }
        defaults {
            publications('aar')
            publishArtifacts = true
            publishPom = true
        }
    }
}

task generateMySources(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}
task addMySourcesToAar(type: Jar) {
    archiveFileName.set("${libraryArtifactId}WithSources.aar")
    destinationDirectory.set(file("build"))
    from zipTree("build/outputs/aar/${libraryArtifactId}-${buildVariant}.aar")
    from fileTree("build").include("libs/${libraryArtifactId}-sources.jar")
}
afterEvaluate { project ->
    project.tasks.preBuild.dependsOn generateMySources
    project.addMySourcesToAar.dependsOn build
}
artifacts {
    archives addMySourcesToAar.archiveFile.get().asFile
}

artifactoryPublish {
    doFirst {
        uploadGitHash(libraryArtifactId)
    }
}